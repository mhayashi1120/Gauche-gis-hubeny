(define-module gis.hubeny
  (use util.match)
  (use gauche.parameter)
  (use math.const)
  (export
   ;; Parameter
   default-geodesic-datum

   ;; Method
   distance-between
   longitude-limit latitude-limit
   longitude-range latitude-range))
(select-module gis.hubeny)

(define (degree->radian deg)
  (* deg pi/180))

(define (radian->degree radian)
  (* radian 180/pi))

;; http://yamadarake.jp/trdi/report000001.html

;; d = sqrt( (Dy * M)^2 + (Dx * N * cosμy)^2 )

;; d = METRE
;; ( x1, y1 ) = location1
;; ( x2, y2 ) = location2
;; Dy = y1 - y2 (diff latitude)
;; Dx = x1 - x2 (diff longitude)
;; μy = ( y1 + y2 ) / 2 (average of latitude)
;; M = (a * ( 1 - e^2 ) ) / W^3
;; N = a / W
;; W = sqrt ( 1 - ( e^2 * (sinμy)^2 ) )
;; e = sqrt ( ( a^2 - b^2 ) / a^2)

;; Transform formula:

;; Dy = ( sqrt ( d^2 - ( Dx * N * cosμy )^2 ) ) / M
;; Dx = sqrt ( ( d^2 - ( Dy * M )^2 ) / ( N * cosμy ))

;; μ =: MU

;; BESSEL / GRS80 / WGS84
(define default-geodesic-datum
  (make-parameter 'WGS84))

(define-constant BESSEL_CONSTANTS
  '@BESSEL_CONSTANTS@)

(define-constant GRS80_CONSTANTS
  '@GRS80_CONSTANTS@)

(define-constant WGS84_CONSTANTS
  '@WGS84_CONSTANTS@)

(define (define-constants type)
  (apply values
         (ecase type
	       ['BESSEL BESSEL_CONSTANTS]
	       ['GRS80 GRS80_CONSTANTS]
	       ['WGS84 WGS84_CONSTANTS])))

(define (compute-ellipse lat)
  (define-values (a e^2 M-num) (define-constants 'WGS84))

  (let* ([MUy (degree->radian lat)]
         [sinMUy^2 (expt (sin MUy) 2)]
         [W (sqrt (- 1.0 (* e^2 sinMUy^2)))]
         [M (/ M-num (expt W 3))]
         [N (/ a W)]
         [Ncos (* N (cos MUy))])
    (values M Ncos)))

(define (parse-lat/long s)
  (cond
   [(#/^([-+]?[0-9.]+)[\s\t]*,[\s\t]*([-+]?[0-9.]+)$/ s) =>
    (^m (values (string->number (m 1)) (string->number (m 2))))]
   [else
    (errorf "Not matched ~a" s)]))

;;;
;;; API
;;;

(define-method distance-between ((lat/long1 <string>) (lat/long2 <string>))
  (distance-between lat/long1 lat/long2 (default-geodesic-datum)))

;; e.g. lat/long "36.1030203, 139.40202"
(define-method distance-between ((lat/long1 <string>) (lat/long2 <string>))

  (receive (lat1 long1) (parse-lat/long lat/long1)
    (receive (lat2 long2) (parse-lat/long lat/long2)
      (distance-between lat1 long1 lat2 long2))))

(define-method distance-between ((lat1 <number>) (long1 <number>)
                                 (lat2 <number>) (long2 <number>))

  (define-values (M Ncos) (compute-ellipse (/ (+ lat1 lat2) 2.0)))

  (let* ([Dy (degree->radian (- lat1 lat2))]
         [Dx (degree->radian (- long1 long2))]
         [DyM (* Dy M)]
         [DxNcos (* Dx Ncos)]
         [metre (sqrt (+ (expt DyM 2) (expt DxNcos 2)))])
    metre))

;; These methods consider to use search range of lat,long set.

(define-method distance-limit ((d <number>) (lat/long <string>))
  (receive (lat long) (parse-lat/long lat/long)
    (distance-limit d lat long)))

(define-method distance-limit ((d <number>) (lat <number>) (long <number>))
  (values (latitude-limit d lat long) (longitude-limit d lat long)))

(define-method latitude-limit ((d <number>) (lat <number>) (long <number>))
  (define-values (M Ncos) (compute-ellipse lat))

  (let* ([Dx 0.0]
         [Dy (/ (sqrt (-  (expt d 2) (expt (* Dx Ncos) 2))) M)])

    (radian->degree Dy)))

(define-method longitude-limit ((d <number>) (lat <number>) (long <number>))
  (define-values (M Ncos) (compute-ellipse lat))

  (let* ([Dy 0.0]
         [Dx (/ (sqrt (- (expt d 2) (expt (* Dy M) 2))) Ncos)])

    (radian->degree Dx)))

;;TODO improve interface. at polar regions

(define-method latitude-range ((d <number>) (lat <number>) (long <number>))
  (let1 yDegree (latitude-limit d lat long)
    (values (- lat yDegree) (+ lat yDegree))))

(define-method longitude-range ((d <number>) (lat <number>) (long <number>))
  (let1 xDegree (longitude-limit d lat long)
    (values (- long xDegree) (+ long xDegree))))

