(define-module gis.hubeny
  (use util.match)
  (use gauche.parameter)
  (use math.const)
  (export
   ;; Parameter
   default-geodesic-datum
   )
  (export
   hubeny-distance hubeny-distance*
   hubeny-limit hubeny-limit*
   hubeny-lat-limit hubeny-lng-limit
   hubeny-lat-range hubeny-lng-range)
  ;; Deprecating interface
  (export
   ;; Method
   distance-between
   longitude-limit latitude-limit
   longitude-range latitude-range))
(select-module gis.hubeny)

(define (degree->radian deg)
  (* deg pi/180))

(define (radian->degree radian)
  (* radian 180/pi))

;; http://yamadarake.jp/trdi/report000001.html

;; d = sqrt( (Dy * M)^2 + (Dx * N * cosμy)^2 )

;; d = METRE
;; ( x1, y1 ) = location1
;; ( x2, y2 ) = location2
;; Dy = y1 - y2 (diff latitude)
;; Dx = x1 - x2 (diff longitude)
;; μy = ( y1 + y2 ) / 2 (average of latitude)
;; M = (a * ( 1 - e^2 ) ) / W^3
;; N = a / W
;; W = sqrt ( 1 - ( e^2 * (sinμy)^2 ) )
;; e = sqrt ( ( a^2 - b^2 ) / a^2)

;; Transform formula:

;; Dy = ( sqrt ( d^2 - ( Dx * N * cosμy )^2 ) ) / M
;; Dx = sqrt ( ( d^2 - ( Dy * M )^2 ) / ( N * cosμy ))

;; μ =: MU

;; BESSEL / GRS80 / WGS84
(define default-geodesic-datum
  (make-parameter 'WGS84))

(define-constant BESSEL_CONSTANTS
  '@BESSEL_CONSTANTS@)

(define-constant GRS80_CONSTANTS
  '@GRS80_CONSTANTS@)

(define-constant WGS84_CONSTANTS
  '@WGS84_CONSTANTS@)

(define (define-constants type)
  (apply values
         (ecase type
	       ['BESSEL BESSEL_CONSTANTS]
	       ['GRS80 GRS80_CONSTANTS]
	       ['WGS84 WGS84_CONSTANTS])))

(define (compute-ellipse lat)
  (define-values (a e^2 M-num) (define-constants 'WGS84))

  (let* ([MUy (degree->radian lat)]
         [sinMUy^2 (expt (sin MUy) 2)]
         [W (sqrt (- 1.0 (* e^2 sinMUy^2)))]
         [M (/ M-num (expt W 3))]
         [N (/ a W)]
         [Ncos (* N (cos MUy))])
    (values M Ncos)))

(define (parse-lat/long s)
  (cond
   [(#/^([-+]?[0-9.]+)[\s\t]*,[\s\t]*([-+]?[0-9.]+)$/ s) =>
    (^m (values (string->number (m 1)) (string->number (m 2))))]
   [else
    (errorf "Not matched ~a" s)]))

;;;
;;; # Deprecating API
;;;

;; ## Deprecating interface should use [=hubeny-distance]()
;; e.g. lat/long "36.1030203, 139.40202"
;; -> <number>
(define-method distance-between ((lat/long1 <string>) (lat/long2 <string>))

  (hubeny-distance lat/long1 lat/long2))

;; ## Deprecating interface should use [=hubeny-distance*]()
(define-method distance-between ((lat1 <number>) (long1 <number>)
                                 (lat2 <number>) (long2 <number>))
  (hubeny-distance* lat1 long1 lat2 long2))

;; These methods consider to use search range of lat,long set.

;; ## Deprecating interface should use [=hubeny-limit]()
(define-method distance-limit ((d <number>) (lat/long <string>))
  (hubeny-limit d lat/long))

;; ## Deprecating interface should use [=hubeny-limit*]()
(define-method distance-limit ((d <number>) (lat <number>) (long <number>))
  (hubeny-limit* d lat long))

;; ## Deprecating interface should use [=hubeny-lat-limit]()
(define-method latitude-limit ((d <number>) (lat <number>) (_ <number>))
  (hubeny-lat-limit d lat))

;; ## Deprecating interface should use TODO [=hubeny-lng-limit]()
(define-method longitude-limit ((d <number>) (lat <number>) (_ <number>))
  (hubeny-lng-limit d lat))

;;TODO improve interface. at polar regions

;; ## Deprecating interface should use [=hubeny-lat-range]()
(define-method latitude-range ((d <number>) (lat <number>) (long <number>))
  (hubeny-lat-range d lat long))

;; ## Deprecating interface should use TODO [=hubeny-lng-range]()
(define-method longitude-range ((d <number>) (lat <number>) (long <number>))
  (hubeny-lng-range d lat long))

;;;
;;; # API
;;;

;; ##
;; - {GEO1, GEO2} : <string> | (LAT:<number . LNG:<number>)
;;       (e.g. "36.1030203, 139.40202" (36.1030203 . 139.40202))
;; -> <number>
(define (hubeny-distance geo1 geo2)
  (define (coerce-lat&lng g)
    (match g
      [(? string? s)
       (parse-lat/long s)]
      [((? integer? lat) . (? integer? lng))
       (values lat lng)]
      [_
       (error "Not a suported geo" g)]))

  (receive (lat1 long1) (coerce-lat&lng geo1)
    (receive (lat2 long2) (coerce-lat&lng geo2)
      (hubeny-distance* lat1 long1 lat2 long2))))

;; ##
;; - {LAT1, LNG1, LAT2, LNG2} : <number>
;; -> <number>
(define (hubeny-distance* lat1 lng1 lat2 lng2)
  (define-values (M Ncos) (compute-ellipse (/ (+ lat1 lat2) 2.0)))

  (assume-type lat1 <number>)
  (assume-type lng1 <number>)
  (assume-type lat2 <number>)
  (assume-type lng2 <number>)

  (let* ([Dy (degree->radian (- lat1 lat2))]
         [Dx (degree->radian (- lng1 lng2))]
         [DyM (* Dy M)]
         [DxNcos (* Dx Ncos)]
         [metre (sqrt (+ (expt DyM 2) (expt DxNcos 2)))])
    metre))

;; ##
;; - METER : <number>
;; - GEO : <string>
;; -> <number>
(define (hubeny-limit meter geo)
  (receive (lat long) (parse-lat/long geo)
    (hubeny-limit* meter lat long)))

;; ##
;; - METER : <number>
;; - {LAT, LNG} : <number>
;; -> [LAT:<number>, LNG:<number>]
(define (hubeny-limit* meter lat lng)
  (values (hubeny-lat-limit meter lat)
          (hubeny-lng-limit meter lat)))

;; ## This interface still testing
;; - METER : <number>
;; - {LAT, LNG} : <number>
;; -> LATITUDE:<number>
(define (hubeny-lat-limit meter lat)
  (define-values (M Ncos) (compute-ellipse lat))

  (assume-type meter <number>)
  (assume-type lat <number>)

  (let* ([Dx 0.0]
         [Dy (/ (sqrt (-  (expt meter 2) (expt (* Dx Ncos) 2))) M)])

    (radian->degree Dy)))

;; ## This interface still testing
;; -> LONGITUDE:<number>
(define (hubeny-lng-limit meter lat)
  (define-values (M Ncos) (compute-ellipse lat))

  (assume-type meter <number>)
  (assume-type lat <number>)

  (let* ([Dy 0.0]
         [Dx (/ (sqrt (- (expt meter 2) (expt (* Dy M) 2))) Ncos)])

    (radian->degree Dx)))

;; ## This interface still testing
;; -> [LAT-MIN:<number>, LAT-MAX:<number>]
(define (hubeny-lat-range meter lat lng)
  (let1 yDegree (hubeny-lat-limit meter lat)
    (values (- lat yDegree) (+ lat yDegree))))

;; ## This interface still testing
;; -> [LNG-MIN:<number>, LNG-MAX:<number>]
(define (hubeny-lng-range meter lat lng)
  (let1 xDegree (hubeny-lng-limit meter lat)
    (values (- lng xDegree) (+ lng xDegree))))
